<!-- 
```agda
open import Cat.Natural.Whiskering
open import Cat.Displayed.Functor
open import Cat.Displayed.Total
open import Cat.Displayed.Base
open import Cat.Functor.Base
open import Cat.Prelude
```
-->

```agda
module Cat.Displayed.Functor.Total where
```

# Total functor {defines="total-functor"}

Given [[displayed categories|displayed category]] $\cE \liesover \cA$ and 
$\cF \liesover \cB$, and a [[displayed functor]] $F' : \cE \to \cF$ over 
$F : \cA \to \cB$, we can recover an ordinary [[functor]] 
$\int F : \int \cE \to \int \cF$ between [[total categories|total category]]:

<!-- 
```agda
module _
  {oa â„“a ob â„“b oe â„“e of â„“f}
  {A : Precategory oa â„“a} {B : Precategory ob â„“b}
  {â„° : Displayed A oe â„“e} {â„± : Displayed B of â„“f}
  {F : Functor A B} (F' : Displayed-functor F â„° â„±)
  where
```
-->

```agda
  âˆ«á¶  : Functor (âˆ« â„°) (âˆ« â„±)
  âˆ«á¶  = record where
    open Functor F
    open Displayed-functor F'

    Fâ‚€ (x , x') = Fâ‚€ x , Fâ‚€' x'
    Fâ‚ (âˆ«hom f f') = âˆ«hom (Fâ‚ f) (Fâ‚' f')
    F-id = âˆ«Hom-path â„± F-id F-id'
    F-âˆ˜ (âˆ«hom f f') (âˆ«hom g g') = âˆ«Hom-path â„± (F-âˆ˜ f g) F-âˆ˜'
```

The total functor respects the projection `Ï€á¶ `{.Agda} to the base 
category so that

~~~{.quiver .attach-around}
\begin{tikzcd}
	{\int \cE} && {\int \cF} \\
	\\
	\cA && \cB
	\arrow["{\int F'}", from=1-1, to=1-3]
	\arrow["{\pi_{\cE}}"', from=1-1, to=3-1]
	\arrow["{\pi_\cF}", from=1-3, to=3-3]
	\arrow["F"', from=3-1, to=3-3]
\end{tikzcd}
~~~

commutes.

```agda
  âˆ«á¶ -preserves-base : F Fâˆ˜ (Ï€á¶  â„°) â‰¡ (Ï€á¶  â„±) Fâˆ˜ âˆ«á¶ 
  âˆ«á¶ -preserves-base = Functor-path (Î» x â†’ refl) (Î» f â†’ refl)
```

Indeed, a displayed functor $F'$ over $F$ can be thought of as a 
repackaging of the data of a functor $\int F'$ for which this diagram
commutes.

The total functor of the displayed identity functor `Id'`{.Agda} is of
course the ordinary identity functor `Id`{.Agda}.

<!-- 
```agda
module _
  {oa â„“a oe â„“e}
  {A : Precategory oa â„“a} {â„° : Displayed A oe â„“e}
  where
```
-->

```agda
  âˆ«á¶ Id'â‰¡Id : âˆ«á¶  (Id' {â„° = â„°}) â‰¡ Id
  âˆ«á¶ Id'â‰¡Id = Functor-path (Î» x â†’ refl) Î» f â†’ refl
```

Similarly, the composite of two total functors is the total of the
composite.

<!--
```agda
module _
  {oa â„“a ob â„“b oc â„“c oe â„“e of â„“f og â„“g}
  {A : Precategory oa â„“a} {B : Precategory ob â„“b} {C : Precategory oc â„“c}
  {â„° : Displayed A oe â„“e} {â„± : Displayed B of â„“f} {ğ’¢ : Displayed C og â„“g}
  {F : Functor B C} {G : Functor A B}
  {F' : Displayed-functor F â„± ğ’¢} {G' : Displayed-functor G â„° â„±}
  where
```
-->

```agda
  âˆ«á¶ âˆ˜ : âˆ«á¶  (F' Fâˆ˜' G') â‰¡ âˆ«á¶  F' Fâˆ˜ âˆ«á¶  G'
  âˆ«á¶ âˆ˜ = Functor-path (Î» x â†’ refl) Î» f â†’ refl
```

## Total natural transformations {defines="total-natural-transformation"}

Suppose we have an additional [[displayed functor]] $G' : \cE \to \cF$
over $G : \cA \to \cB$, and a [[displayed natural transformation]]
$\eta' : F' \To G'$ over $\eta : F \To G$. We can then similarly recover
an ordinary [[natural transformation]] $\int \eta : \int F \To \int G$ 
between [[total functor|total functors]]:

<!-- 
```agda
module _
  {oa â„“a ob â„“b oe â„“e of â„“f}
  {A : Precategory oa â„“a} {B : Precategory ob â„“b}
  {â„° : Displayed A oe â„“e} {â„± : Displayed B of â„“f}
  {F G : Functor A B} {Î·â¿ : F => G}
  {F' : Displayed-functor F â„° â„±}
  {G' : Displayed-functor G â„° â„±}
  (Î·'â¿ : F' =[ Î·â¿ ]=> G')
  where

  open _=>_ Î·â¿
  open _=[_]=>_ Î·'â¿
```
-->

```agda
  âˆ«â¿ : âˆ«á¶  F' => âˆ«á¶  G'
  âˆ«â¿ = record where
    Î· (x , x') = âˆ«hom (Î· x) (Î·' x')
    is-natural (x , x') (y , y') (âˆ«hom f f') = âˆ«Hom-path â„± 
      (is-natural x y f) (is-natural' x' y' f')
```

Applying the projection `Ï€á¶ `{.Agda} to the total natural transformation
$\int\eta'$ gives back $\eta$ in the following sense:

```agda
  âˆ«â¿-preserves-base : PathP 
    (Î» i â†’ âˆ«á¶ -preserves-base F' i => âˆ«á¶ -preserves-base G' i) 
    (Î·â¿ â—€ Ï€á¶  â„°) (Ï€á¶  â„± â–¶ âˆ«â¿)
  âˆ«â¿-preserves-base = Nat-pathp 
    (âˆ«á¶ -preserves-base F') (âˆ«á¶ -preserves-base G') Î» x â†’ refl
```
